apply plugin: 'com.android.application'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.0"
    defaultConfig {
        applicationId "io.github.t3r1jj.pbmap"
        minSdkVersion 14
        targetSdkVersion 25
        versionCode buildVersionCode()
        versionName version
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        setProperty("archivesBaseName", "io.github.t3r1jj.pbmap-$versionName")
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    buildTypes.each {
        it.buildConfigField 'String', 'ASSETS_MAP_DIR', ASSETS_MAP_DIR
        it.buildConfigField 'String', 'FIRST_MAP_FILENAME', FIRST_MAP_FILENAME
        it.buildConfigField 'String', 'NOTICE_PATH', NOTICE_PATH
        it.buildConfigField 'String', 'FONT_PATH', FONT_PATH
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:25.3.0'
    compile 'com.android.support:design:25.3.0'
    compile 'com.android.support:cardview-v7:25.3.0'
    compile 'com.qozix:tileview:2.2.6'
    compile('org.simpleframework:simple-xml:2.7.1') {
        exclude module: 'stax'
        exclude module: 'stax-api'
        exclude module: 'xpp3'
    }
    compile 'com.roughike:swipe-selector:1.0.6'
    compile 'com.github.clans:fab:1.6.4'
    compile 'com.getkeepsafe.taptargetview:taptargetview:1.9.1'

    testCompile 'junit:junit:4.12'
}

task copyNOTICE(type: Copy) {
    description 'Copying NOTICE.html to assets for display in app'
    from '../'
    into 'src/main/assets'
    include 'NOTICE.html'
}

preBuild.dependsOn copyNOTICE
